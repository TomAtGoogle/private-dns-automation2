#
# Copyright 2020 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

#<!--* freshness: { owner: 'ttaggart@google.com' reviewed: '2022-nov-23' } *-->


resource "google_compute_backend_bucket" "images" {
  project     = var.private_dns_pid
  name        = "images-backend-bucket"
  bucket_name = google_storage_bucket.images.name
  enable_cdn  = false
}

resource "google_storage_bucket" "images" {
  project  = var.private_dns_pid
  name     = var.bucketname
  location = "US"
}

resource "google_compute_global_address" "lb" {
  name    = "global-lb-ip"
  project = var.private_dns_pid

  depends_on = [
    # The project's services must be set up before the
    # resource is enabled as the compute API will not
    # be enabled and cause the setup to fail.
    google_project_service.compute_api,
  ]
}

resource "google_compute_url_map" "http_lb" {
  project         = var.private_dns_pid
  name            = "http-lb"
  default_service = google_compute_backend_bucket.images.self_link

  depends_on = [
    # The project's services must be set up before the
    # resource is enabled as the compute API will not
    # be enabled and cause the setup to fail.
    google_project_service.compute_api,
  ]
}

resource "google_compute_target_http_proxy" "http_ib_proxy" {
  project = var.private_dns_pid
  name    = "http-lb-proxy"
  url_map = google_compute_url_map.http_lb.self_link

  depends_on = [
    # The project's services must be set up before the
    # resource is enabled as the compute API will not
    # be enabled and cause the setup to fail.
    google_project_service.compute_api,
  ]
}

resource "google_compute_global_forwarding_rule" "privateDNS_gfr" {
  project    = var.private_dns_pid
  name       = "private-dns-gfr"
  target     = google_compute_target_http_proxy.http_ib_proxy.self_link
  port_range = "80"
  ip_address = google_compute_global_address.lb.self_link

  depends_on = [
    # The project's services must be set up before the
    # resource is enabled as the compute API will not
    # be enabled and cause the setup to fail.
    google_project_service.compute_api,
  ]
}


